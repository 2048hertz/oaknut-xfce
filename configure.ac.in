dnl configure.ac
dnl
dnl xfce4 - panel for the Xfce Desktop environment
dnl
dnl 2005-2006 Jasper Huijsmans <jasper@xfce.org>
dnl

dnl library version info
m4_define([libxfce4panel_verinfo], [2:1:1])

dnl panel version info
m4_define([xfce4_panel_version_major], [4])
m4_define([xfce4_panel_version_minor], [3])
m4_define([xfce4_panel_version_micro], [99])
m4_define([xfce4_panel_version_nano],  [3])   dnl leave this empty to have no nano version
m4_define([xfce4_panel_version_build], [@REVISION@])
m4_define([xfce4_panel_version_tag],   [svn])

m4_define([xfce4_panel_version], [xfce4_panel_version_major().xfce4_panel_version_minor().xfce4_panel_version_micro()ifelse(xfce4_panel_version_nano(), [], [], [.xfce4_panel_version_nano()])ifelse(xfce4_panel_version_tag(), [svn], [xfce4_panel_version_tag()-xfce4_panel_version_build()], [xfce4_panel_version_tag()])])



dnl init autoconf
AC_COPYRIGHT([Copyright (c) 2002-2006
        The Xfce development team. All rights reserved.

Written for Xfce by Jasper Huijsmans <jasper@xfce.org>.])
AC_INIT([xfce4-panel], [xfce4_panel_version()], [xfce4-dev@xfce.org])

dnl init automake
XFCE4_PANEL_VERSION=xfce4_panel_version()
AM_INIT_AUTOMAKE([xfce4-panel], [$XFCE4_PANEL_VERSION])
AM_CONFIG_HEADER([config.h])
AM_MAINTAINER_MODE

dnl Check for UNIX variants
AC_AIX
AC_ISC_POSIX
AC_MINIX
AM_CONDITIONAL([HAVE_CYGWIN], [test "`uname | grep \"CYGWIN\"`" != ""])

dnl disable static libs
AC_DISABLE_STATIC

dnl Check for basic programs
AC_PROG_CC()
AC_PROG_INSTALL()
AC_PROG_LN_S()
AC_PROG_LIBTOOL()

AC_PROG_INTLTOOL([0.31], [no-xml])

dnl patch intltool-update to support *.rc.in config files
AC_CONFIG_COMMANDS([intltool-update],
	[(sed -e 's/^\("desktop.*".*\)/\1\n"rc(?:\\\\.in)+|"\./' intltool-update > intltool-update-tmp && 
	  mv intltool-update-tmp intltool-update && chmod 755 intltool-update)])

LIBXFCE4PANEL_VERINFO=libxfce4panel_verinfo()
AC_SUBST([LIBXFCE4PANEL_VERINFO])

dnl Check for standard header files
AC_HEADER_STDC
AC_CHECK_HEADERS([signal.h stddef.h sys/wait.h time.h string.h sys/mman.h])
AC_CHECK_FUNCS([sigaction])
AC_FUNC_MMAP()

dnl Check for i18n support
XDT_I18N([@LINGUAS@])

dnl Check for X11 installed
XDT_CHECK_LIBX11_REQUIRE
XDT_CHECK_LIBSM

dnl Check for required packages
XDT_CHECK_PACKAGE([LIBXFCE4UTIL], [libxfce4util-1.0], [4.3.99.2])
XDT_CHECK_PACKAGE([GTHREAD], [gthread-2.0], [2.6.0])
XDT_CHECK_PACKAGE([GTK], [gtk+-2.0], [2.6.0])
XDT_CHECK_PACKAGE([LIBXFCEGUI4], [libxfcegui4-1.0], [4.3.99.2])
XDT_CHECK_PACKAGE(LIBXFCE4MCS_MANAGER, libxfce4mcs-manager-1.0, 4.3.99.2)
XFCE_MCS_PLUGIN([XFCE_MCS_MANAGER], [4.3.99.1])

dnl Check for optional packages
XDT_CHECK_OPTIONAL_PACKAGE([LIBSTARTUP_NOTIFICATION],
			   [libstartup-notification-1.0], [0.5],
			   [startup-notification], 
			   [startup notification library], [yes])

dnl install dummy scripts?
AC_ARG_ENABLE([dummy-scripts],
        AC_HELP_STRING([--enable-dummy-scripts],
                [Install empty scripts for the deprecated xftaskbar4 
		 and xfce4-iconbox (default=enabled)]),
        [ac_cv_enable_dummy_scripts=$enableval],
        [ac_cv_enable_dummy_scripts=yes])
if test "x$ac_cv_enable_dummy_scripts" = "xno"; then
        DUMMY_SCRIPTS=""
else
        DUMMY_SCRIPTS="xftaskbar4 xfce4-iconbox"
fi
AC_SUBST(DUMMY_SCRIPTS)

dnl gtk-doc
GTK_DOC_CHECK([1.0])

dnl Check for debugging support
XDT_FEATURE_DEBUG()

AC_OUTPUT([
xfce4-panel.spec
Makefile
config/Makefile
icons/Makefile
libxfce4panel/Makefile
libxfce4panel/libxfce4panel-1.0.pc
panel/Makefile
plugins/actions/Makefile
plugins/clock/Makefile
plugins/iconbox/Makefile
plugins/launcher/Makefile
plugins/Makefile
plugins/pager/Makefile
plugins/separator/Makefile
plugins/showdesktop/Makefile
plugins/systray/Makefile
plugins/tasklist/Makefile
plugins/windowlist/Makefile
mcs-plugin/Makefile
po/Makefile.in
docs/Makefile
docs/API/Makefile
docs/API/version.xml
docs/manual/Makefile
docs/manual/C/Makefile
docs/manual/C/images/Makefile
])

